<%--  -*- sparql -*- --%>
<%@ val uri: String = "http://nothing.com" %>
${include("sparql/prefixes.ssp")}

SELECT 
  ?uri ?type ?label ?organizationUri ?institution 
  ?dateTimeUri ?endUri ?endDate
  ?startUri ?startDate
  ?degreeUri ?degree
  ("${uri}" AS ?personUri)
WHERE
{
  <${uri}> obo:RO_0000056 ?uri .
  ?uri a core:EducationalProcess ;
    vitro:mostSpecificType ?type ;
    rdfs:label ?label .
  OPTIONAL {
    ?uri obo:RO_0000057 ?organizationUri .
    ?organizationUri rdfs:label ?institution ; 
      a foaf:Organization.
  }
  OPTIONAL {
    ?uri core:dateTimeInterval ?dateTimeUri .
    ?dateTimeUri core:end ?endUri .
    ?endUri core:dateTime ?endDate . 
  }
  OPTIONAL {
    ?dateTimeUri core:start ?startUri .
    ?startUri core:dateTime ?startDate . 
  }
  OPTIONAL {
    ?uri obo:RO_0002234 ?awardedDegree .
    ?awardedDegree core:relates ?degreeUri .
    ?degreeUri a core:AcademicDegree ;
      core:abbreviation ?degree . 
  }
}
order by ?startDate ?endDate
