<%--  -*- sparql -*- --%>
<%@ val uri: String = "http://nothing.com" %>
${include("sparql/prefixes.ssp")}

select *
where {
  <${uri}> core:authorInAuthorship ?authorship .
  ?publication core:informationResourceInAuthorship ?authorship .
  ?publication rdfs:label ?label .
  ?publication vitro:mostSpecificType ?type .
  OPTIONAL { ?publication bibo:numPages ?numPages . }
  OPTIONAL { ?publication bibo:edition ?edition . }
  OPTIONAL { ?publication bibo:volume ?volume . }
  OPTIONAL { ?publication bibo:issue ?issue . }
  OPTIONAL { 
    ?publication core:hasPublicationVenue ?publicationVenue . 
    ?publicationVenue rdfs:label ?publishedIn .
  }
  OPTIONAL { ?publication duke:publicationVenue ?publishedIn }
  OPTIONAL { ?publication duke:authorList ?authorList }
  OPTIONAL { 
    ?publication core:publisher ?publisher. ?publisher rdfs:label ?publishedBy .
  }
  OPTIONAL { ?publication bibo:pageStart ?startPage .}
  OPTIONAL { ?publication bibo:pageEnd ?endPage .}
  OPTIONAL { 
    ?publication core:dateTimeValue ?datetime . ?datetime core:dateTime ?year .
  }
  OPTIONAL {?authorship duke:isFavoritedAuthorship ?isFavorite .}
  OPTIONAL {?publication duke:parentBookTitle ?parentBookTitle .}
  OPTIONAL {?publication bibo:doi ?doi .}
  OPTIONAL {?publication bibo:abstract ?abstract .}
  OPTIONAL {?publication bibo:pmid ?pmid .}
  OPTIONAL {?publication core:pmcid ?pmcid .}
  OPTIONAL {?publication duke:source ?publicationSource .}
  OPTIONAL {?publication duke:subtypes ?subtypes}
  OPTIONAL {?publication duke:chicagoCitation ?chicago_citation.}
} 
order by desc(?datetime)
