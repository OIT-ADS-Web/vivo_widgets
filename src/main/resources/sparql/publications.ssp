<%--  -*- sparql -*- --%>
<%@ val uri: String = "http://nothing.com" %>
${include("sparql/prefixes.ssp")}

SELECT ?publication (MIN(?label) AS ?label) (MIN(?type) AS ?type)
 (MIN(?numPages) AS ?numPages) (MIN(?edition) AS ?edition)
 (MIN(?volume) AS ?volume) (MIN(?issue) AS ?issue) (MIN(?isbn10) AS ?isbn10)
 (MIN(?isbn13) AS ?isbn13) (MIN(?publicationVenue) AS ?publicationVenue)
 (MIN(?publishedIn) AS ?publishedIn) (MIN(?authorList) AS ?authorList)
 (MIN(?editorList) AS ?editorList) (MIN(?translatorList) AS ?translatorList)
 (MIN(?publishedBy) AS ?publishedBy) (MIN(?startPage) AS ?startPage)
 (MIN(?endPage) AS ?endPage) (MIN(?datetime) AS ?datetime) (MIN(?year) AS ?year)
 (MIN(?isFavorite) AS ?isFavorite) (MIN(?parentBookTitle) AS ?parentBookTitle)
 (MIN(?doi) AS ?doi) (MIN(?abstract) AS ?abstract) (MIN(?pmid) AS ?pmid)
 (MIN(?pmcid) AS ?pmcid) (MIN(?publicationSource) AS ?publicationSource)
 (MIN(?subtypes) AS ?subtypes) (MIN(?conferenceName) AS ?conferenceName)
 (MIN(?conferenceLocation) AS ?conferenceLocation) (MIN(?chicagoCitation) AS ?chicagoCitation)
 (MIN(?mlaCitation) AS ?mlaCitation) (MIN(?apaCitation) AS ?apaCitation)
 (MIN(?icmjeCitation) AS ?icmjeCitation) (MIN(?authorshipType) AS ?authorshipType)
 (MIN(?conferenceDatetimeStart) AS ?conferenceDatetimeStart) (MIN(?conferenceDatetimeFinish) AS ?conferenceDatetimeFinish)
 (MIN(?conferenceStartDate) AS ?conferenceStartDate) (MIN(?conferenceFinishDate) AS ?conferenceFinishDate)
 (MIN(MONTH(?conferenceStartDate)) AS ?conferenceStartDateMonth)
 (MIN(YEAR(?conferenceStartDate)) AS ?conferenceStartDateYear)
 (MIN(DAY(?conferenceStartDate)) AS ?conferenceStartDateDay)
 (MIN(MONTH(?conferenceFinishDate)) AS ?conferenceFinishDateMonth)
 (MIN(YEAR(?conferenceFinishDate)) AS ?conferenceFinishDateYear)
 (MIN(DAY(?conferenceFinishDate)) AS ?conferenceFinishDateDay)

WHERE {
  <${uri}> core:relatedBy ?authorship .
  {?authorship a core:Authorship.}
  UNION
  {?authorship a core:Translatorship.}
  UNION
  {?authorship a core:Editorship.}
  ?authorship vitro:mostSpecificType ?authorshipType .
  ?authorship core:relates ?publication .
  ?publication a obo:IAO_0000030 .
  ?publication rdfs:label ?label .
  ?publication vitro:mostSpecificType ?type .
  OPTIONAL { ?publication bibo:numPages ?numPages . }
  OPTIONAL { ?publication bibo:edition ?edition . }
  OPTIONAL { ?publication bibo:volume ?volume . }
  OPTIONAL { ?publication bibo:issue ?issue . }
  OPTIONAL { ?publication bibo:isbn10 ?isbn10 . }
  OPTIONAL { ?publication bibo:isbn13 ?isbn13 . }
  OPTIONAL { 
    ?publication core:hasPublicationVenue ?publicationVenue . 
    ?publicationVenue rdfs:label ?publishedIn .
  }
  OPTIONAL { ?publication duke:publicationVenue ?publishedIn }
  OPTIONAL { ?publication duke:authorList ?authorList }
  OPTIONAL { ?publication duke:editorList ?editorList }
  OPTIONAL { ?publication duke:translatorList ?translatorList }
  OPTIONAL { 
    ?publication core:publisher ?publisher. ?publisher rdfs:label ?publishedBy .
  }
  OPTIONAL { ?publication bibo:pageStart ?startPage .}
  OPTIONAL { ?publication bibo:pageEnd ?endPage .}
  OPTIONAL { 
    ?publication core:dateTimeValue ?datetime . ?datetime core:dateTime ?year .
  }
  OPTIONAL {?authorship duke:isFavorite ?isFavorite .}
  OPTIONAL {?publication duke:parentBookTitle ?parentBookTitle .}
  OPTIONAL {?publication bibo:doi ?doi .}
  OPTIONAL {?publication bibo:abstract ?abstract .}
  OPTIONAL {?publication bibo:pmid ?pmid .}
  OPTIONAL {?publication core:pmcid ?pmcid .}
  OPTIONAL {?publication duke:source ?publicationSource .}
  OPTIONAL {?publication duke:subtypes ?subtypes}
  OPTIONAL { 
    ?publication core:start ?conferenceDatetimeStart . 
    ?conferenceDatetimeStart core:dateTime ?conferenceStartDate .
  }                 
  OPTIONAL { 
    ?publication core:end ?conferenceDatetimeFinish . 
    ?conferenceDatetimeFinish core:dateTime ?conferenceFinishDate .
  }
  OPTIONAL {?publication dukeact:serviceOrEventName ?conferenceName .}  
  OPTIONAL {?publication dukeact:locationOrVenue ?conferenceLocation .} 
  OPTIONAL {?publication duke:chicagoCitation ?chicagoCitation.}
  OPTIONAL {?publication duke:mlaCitation ?mlaCitation.}
  OPTIONAL {?publication duke:apaCitation ?apaCitation.}
  OPTIONAL {?publication duke:icmjeCitation ?icmjeCitation.}
} GROUP BY ?publication
ORDER BY desc(?datetime)
