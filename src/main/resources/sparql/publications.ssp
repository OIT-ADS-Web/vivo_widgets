<%--  -*- sparql -*- --%>
<%@ val uri: String = "http://nothing.com" %>

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX core: <http://vivoweb.org/ontology/core#>
PREFIX duke: <http://vivo.duke.edu/vivo/ontology/duke-extension#>
PREFIX dukeact: <http://vivo.duke.edu/vivo/ontology/duke-activity-extension#>

SELECT ?publication
?label ?type
?numPages ?edition
?volume ?issue ?isbn10
?isbn13 ?publicationVenue
?publishedIn ?authorList
?editorList ?translatorList
?publishedBy ?startPage
?endPage
?datetime
?year
?isFavorite ?parentBookTitle
?doi ?abstract ?pmid
?pmcid ?publicationSource
?subtypes ?conferenceName
?conferenceLocation ?chicagoCitation
?mlaCitation ?apaCitation
?icmjeCitation ?authorshipType
?conferenceDatetimeStart 
?conferenceDatetimeFinish
?conferenceStartDate 
?conferenceFinishDate
(MONTH(?conferenceStartDate) AS ?conferenceStartDateMonth)
(YEAR(?conferenceStartDate) AS ?conferenceStartDateYear)
(DAY(?conferenceStartDate) AS ?conferenceStartDateDay)
(MONTH(?conferenceFinishDate) AS ?conferenceFinishDateMonth)
(YEAR(?conferenceFinishDate) AS ?conferenceFinishDateYear)
(DAY(?conferenceFinishDate) AS ?conferenceFinishDateDay)

WHERE {
  <${uri}> core:relatedBy ?authorship .
  ?authorship vitro:mostSpecificType ?authorshipType .
  ?authorship core:relates ?publication .
  ?publication a obo:IAO_0000030 .
  ?publication rdfs:label ?label .
  ?publication vitro:mostSpecificType ?type .
  OPTIONAL { ?publication bibo:numPages ?numPages . }
  OPTIONAL { ?publication bibo:edition ?edition . }
  OPTIONAL { ?publication bibo:volume ?volume . }
  OPTIONAL { ?publication bibo:issue ?issue . }
  OPTIONAL { ?publication bibo:isbn10 ?isbn10 . }
  OPTIONAL { ?publication bibo:isbn13 ?isbn13 . }
  OPTIONAL {
    ?publication core:hasPublicationVenue ?publicationVenue .
    ?publicationVenue rdfs:label ?publishedIn .
  }
  OPTIONAL { ?publication duke:publicationVenue ?publishedIn }
  OPTIONAL { ?publication duke:authorList ?authorList }
  OPTIONAL { ?publication duke:editorList ?editorList }
  OPTIONAL { ?publication duke:translatorList ?translatorList }
  OPTIONAL {
    ?publication core:publisher ?publisher. ?publisher rdfs:label ?publishedBy .
  }
  OPTIONAL { ?publication bibo:pageStart ?startPage .}
  OPTIONAL { ?publication bibo:pageEnd ?endPage .}
  OPTIONAL {
    ?publication core:dateTimeValue ?datetime . ?datetime core:dateTime ?year .
  }
  OPTIONAL {?authorship duke:isFavorite ?isFavorite .}
  OPTIONAL {?publication duke:parentBookTitle ?parentBookTitle .}
  OPTIONAL {?publication bibo:doi ?doi .}
  OPTIONAL {?publication bibo:abstract ?abstract .}
  OPTIONAL {?publication bibo:pmid ?pmid .}
  OPTIONAL {?publication core:pmcid ?pmcid .}
  OPTIONAL {?publication duke:source ?publicationSource .}
  OPTIONAL {?publication duke:subtypes ?subtypes}

  OPTIONAL {
    ?publication core:start ?conferenceDatetimeStart .
    ?conferenceDatetimeStart core:dateTime ?conferenceStartDate .
  }
  OPTIONAL {
    ?publication core:end ?conferenceDatetimeFinish .
    ?conferenceDatetimeFinish core:dateTime ?conferenceFinishDate .
  }
  OPTIONAL {?publication dukeact:serviceOrEventName ?conferenceName .}  
  OPTIONAL {?publication dukeact:locationOrVenue ?conferenceLocation .}


  OPTIONAL {?publication duke:chicagoCitation ?chicagoCitation.}
  OPTIONAL {?publication duke:mlaCitation ?mlaCitation.}
  OPTIONAL {?publication duke:apaCitation ?apaCitation.}
  OPTIONAL {?publication duke:icmjeCitation ?icmjeCitation.}

  FILTER(?authorshipType IN (core:Authorship, core:Translatorship, core:Editorship))

} 
ORDER BY desc(?datetime)

